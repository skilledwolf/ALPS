#  Copyright Lukas Gamper and Synge Todo 2009 - 2010.
#   Permission is hereby granted, free of charge, to any person obtaining
#   a copy of this software and associated documentation files (the “Software”),
#   to deal in the Software without restriction, including without limitation
#   the rights to use, copy, modify, merge, publish, distribute, sublicense,
#   and/or sell copies of the Software, and to permit persons to whom the
#   Software is furnished to do so, subject to the following conditions:
#  
#   The above copyright notice and this permission notice shall be included
#   in all copies or substantial portions of the Software.
#  
#   THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS
#   OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
#   FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
#   DEALINGS IN THE SOFTWARE.

if(NOT EXISTS "${cmd_path}")
  message(FATAL_ERROR "Test executable does not exist: ${cmd_path}")
endif()
set(input_names "${input}.input" "${input}.ip")
set(output_names "${output}.output" "${output}.op")
find_file(
  input_path
  NAMES ${input_names}
  PATHS "${binarydir}" "${sourcedir}"
  NO_DEFAULT_PATH)
find_file(
  output_path
  NAMES ${output_names}
  PATHS "${binarydir}" "${sourcedir}"
  NO_DEFAULT_PATH)
set(input_args)
if(input_path)
  list(APPEND input_args INPUT_FILE "${input_path}")
endif()
set(actual "${binarydir}/${name}.actual")
set(ENV{OMP_NUM_THREADS} 1)
execute_process(
  COMMAND "${cmd_path}" ${input_args}
  OUTPUT_FILE "${actual}"
  ERROR_VARIABLE error
  RESULT_VARIABLE result
  TIMEOUT 600)
if(NOT result STREQUAL "0")
  message(FATAL_ERROR "${name} failed (${result}): ${error}; output: ${actual}")
endif()
if(output_path)
  execute_process(COMMAND "${CMAKE_COMMAND}" -E compare_files --ignore-eol "${output_path}"
                          "${actual}" RESULT_VARIABLE mismatch)
  if(mismatch)
    message(FATAL_ERROR "${name}: ${actual} differs from ${output_path}")
  endif()
endif()
file(REMOVE "${actual}")
